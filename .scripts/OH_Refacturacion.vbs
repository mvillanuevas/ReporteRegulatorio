On Error Resume Next

Set objArgs = WScript.Arguments

WorkbookPathRef = objArgs(0)
WorkbookPathOH = objArgs(1)
periodo = objArgs(2)
Folio = objArgs(3)

'WorkbookPathRef = "C:\ReporteRegulatorioRpa\Input\Refacturacion_regular_v2 junio 2025.xlsm"
'WorkbookPathOH = "C:\ReporteRegulatorioRpa\Input\REXM - Overhead facturable junio 2025.xlsx"
'periodo = "Junio 2025"
'Folio = "618"

Folio = CInt(Folio)
'Genera un objeto de tipo Excel Application
Set objExcel = CreateObject("Excel.Application")

'Parámetro para indicar si se quiere visible la aplicación de Excel
objExcel.Application.Visible = False
'Evita movimiento de pantalla
objExcel.Application.ScreenUpdating = False
'Parámetro evitar mostrar pop ups de Excel
objExcel.Application.DisplayAlerts = False

Set objWorkbookPathRef = objExcel.Workbooks.Open(WorkbookPathRef, 0)
Set objWorkbookSheetRef= objWorkbookPathRef.Worksheets("OH")

Set objWorkbookPathOH = objExcel.Workbooks.Open(WorkbookPathOH, 0)
Set objWorkbookSheetOH = objWorkbookPathOH.Worksheets("Sheet2")

Const xlPart = 2
Const xlValues = -4163

' Establece rango de busqueda en la columna C
Set nRange = objWorkbookSheetOH.Range("A:A")
' En la columna A de la hoja OH, buscar el valor "BL29" y obtener la fila
Dim foundCell : Set foundCell = nRange.Find("BL29",,xlValues,xlPart)

fechaActual = DateSerial(Year(Date), Month(Date), Day(Date))


If Not foundCell Is Nothing Then
    rowNumber = foundCell.Row
    filaOH = 2
    'Iterar cada 3 columnas a partir de la columna F
    For i = 6 To objWorkbookSheetOH.Cells(rowNumber, objWorkbookSheetOH.Columns.Count).End(-4159).Column Step 3
        ' Verificar si la celda contiene un valor numérico
        If objWorkbookSheetOH.Cells(rowNumber, i).Value <> 0 And  objWorkbookSheetOH.Cells(rowNumber, i).Value <> "-" Then
            objWorkbookSheetRef.Cells(filaOH, 3).Value = Folio
            ' Fecha actual
            'objWorkbookSheetRef.Cells(filaOH, 4).Value = Right("0" & Day(Date), 2) & "/" & Right("0" & Month(Date), 2) & "/" & Year(Date)
            objWorkbookSheetRef.Cells(filaOH, 4).Value = fechaActual
            ' Reemplazar el valor " <<Periodo>>" de la columna 28 por la variable "Periodo"
            objWorkbookSheetRef.Cells(filaOH, 28).Value = Replace(objWorkbookSheetRef.Cells(filaOH, 28).Value, "<<Periodo>>", periodo)
            objWorkbookSheetRef.Cells(filaOH, 29).Value = objWorkbookSheetOH.Cells(rowNumber, i).Value
            Folio = Folio + 1
            filaOH = filaOH + 1
        End If
    Next
    ' Ultima fila
    lastRowOH = objWorkbookSheetRef.Cells(objWorkbookSheetRef.Rows.Count, 3).End(-4162).Row
    objWorkbookSheetRef.Range("A2:B2").AutoFill objWorkbookSheetRef.Range("A2:B" & lastRowOH), 1
    objWorkbookSheetRef.Range("E2:AB2").AutoFill objWorkbookSheetRef.Range("E2:AB" & lastRowOH), 1
    objWorkbookSheetRef.Range("AD2:GC2").AutoFill objWorkbookSheetRef.Range("AD2:GC" & lastRowOH), 1

    objWorkbookSheetRef.Range("D2:D" & lastRowOH).NumberFormat = "dd/mm/yyyy"
    objWorkbookSheetRef.Range("AC2:AC" & lastRowOH).NumberFormat = "#,##0.00"

    ' Guardar y cerrar el libro de refacturación
    objWorkbookPathRef.Save
    objWorkbookPathRef.Close

    objWorkbookPathOH.Save
    objWorkbookPathOH.Close
    ' Cerrar la aplicación de Excel
    objExcel.Quit

Else
    ' Guardar y cerrar el libro de refacturación
    objWorkbookPathRef.Save
    objWorkbookPathRef.Close

    objWorkbookPathOH.Save
    objWorkbookPathOH.Close
    ' Cerrar la aplicación de Excel
    objExcel.Quit
    Msg = "Error was generated by " & "Valor 'BL29' no encontrado en la columna A."
    WScript.StdOut.WriteLine Msg
End If

'Devuelve el error en caso de
If Err.Number <> 0 Then
    ' Guardar y cerrar el libro de refacturación
    objWorkbookPathRef.Save
    objWorkbookPathRef.Close

    objWorkbookPathOH.Save
    objWorkbookPathOH.Close
    ' Cerrar la aplicación de Excel
    objExcel.Quit
    Msg = "Error was generated by " & Err.Source & ". " & Err.Description
    WScript.StdOut.WriteLine Msg
Else
    WScript.StdOut.WriteLine Folio & ":Script executed successfully."
End if
